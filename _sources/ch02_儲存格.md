# Ch02 儲存格

## 047 參照儲存格
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 參照儲存格範圍()
    Range("A3", "G7").Font.Italic = True
    Range("A3:G7").HorizontalAlignment = xlCenter
    Range("表").Borders.LineStyle = xlContinuous
    Range("A1").Font.Bold = True
    Range("A3:G3,A7").Style = "好"
End Sub
```

## 048 參照單一儲存格或所有儲存格
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 參照儲存格()
    Dim i As Integer
    Cells(1, 7).Value = Date
    For i = 2 To 6
        If Cells(7, i).Value >= Cells(7, "G").Value Then
           Cells(7, i).Interior.ColorIndex = 3
        End If
    Next i
    Cells.Font.Size = 12
End Sub
```

## 049 參照作用儲存格
```
Attribute VB_Name = "Module3"
Option Explicit

Sub 作用儲存格()
    Range("A1").Select
    ActiveSheet.Shapes("Picture 1").Select
    MsgBox "作用儲存格: " & ActiveCell.Address _
    & Chr(10) & "選擇: " & TypeName(Selection)
End Sub
```

## 050 選擇儲存格
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 選擇儲存格()
    Range("A3:B11").Select
    Selection.Borders.LineStyle = xlContinuous
    Range("B11").Activate
    ActiveCell.Formula = "=SUM(B4:B10)"
End Sub
```

## 051 以相對位移參照儲存格
```
Attribute VB_Name = "Module3"
Option Explicit

Sub 相對儲存格()
    Range("A3").Offset(7, 0).Select
    Selection.Value = "合計"
    Selection.Offset(0, 1).Formula = "=SUM(B4:B9)"
    Selection.Offset(0, 2).Formula = "=B10/E1"
    Range(Selection, Selection.Offset(0, 2)).Interior.ColorIndex = 36
End Sub
```

## 052 參照已鍵入資料區域的終端儲存格
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 取得終端儲存格()
    Dim sRange As Range, eRange As Range
    Dim i As Integer
    For i = 3 To Range("A2").End(xlDown).Row
        Set sRange = Cells(i, 1)
        Set eRange = sRange.End(xlToRight)
        If i Mod 2 = 0 Then
           Range(sRange, eRange).Interior.ColorIndex = 34
        End If
    Next
    Set sRange = Nothing
    Set eRange = Nothing
End Sub
```

## 053 選取表格的全部區域
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 做成表格()
    Range("A1").End(xlDown).Select
    ActiveCell.CurrentRegion.Borders.LineStyle = xlContinuous
    ActiveCell.End(xlDown).CurrentRegion.Borders.LineStyle = xlContinuous
End Sub
```

## 054 變更儲存格範圍
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 變更儲存格範圍()
    Dim myRange As Range
    Dim myRow As Integer, myCol As Integer
    
    Set myRange = Range("A3").CurrentRegion
    myRow = myRange.Rows.Count
    myCol = myRange.Columns.Count
    Set myRange = myRange.Resize(myRow - 1, myCol - 1)
    
    ActiveSheet.Shapes.AddChart(xlPie, 10, 200).Select
    ActiveChart.SetSourceData Source:=myRange
    Set myRange = Nothing
End Sub
```

## 055 結合多個儲存格範圍
```
Attribute VB_Name = "Module2"
Option Explicit

Sub 結合儲存格範圍()
    Dim Range1 As Range, Range2 As Range
    Dim Range3 As Range, allRange As Range
    
    Set Range1 = Range("A1").CurrentRegion
    Set Range2 = Range("A8").CurrentRegion
    Set Range3 = Range("A13").CurrentRegion
    Set allRange = Union(Range1, Range2, Range3)
    
    allRange.SpecialCells(xlCellTypeFormulas). _
                Interior.Color = RGB(255, 255, 0)
    Set Range1 = Nothing: Set Range2 = Nothing
    Set Range3 = Nothing: Set Range1 = Nothing
End Sub
```

## 056 取得選取範圍的位置
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 取得位址()
    Dim myRange As Range
    Set myRange = Range("A3").CurrentRegion
    MsgBox "儲存格A3的目前區域作用領域: " & myRange.Address(RowAbsolute:=False, _
            ColumnAbsolute:=False, ReferenceStyle:=xlA1, External:=True)
    Set myRange = Nothing
End Sub
```

## 057 取得所選擇儲存格的列和欄的編號
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 取得列欄編號()
    Dim myRow As Long, myColumn As Long
    Dim myRange As Range, i As Integer
    
    myRow = Range("A3").End(xlDown).Row
    myColumn = Range("A3").End(xlToRight).Column
    For i = 4 To myRow
        Set myRange = Cells(i, myColumn)
        If myRange.Value > 1 Then
           myRange.Style = "壞"
        End If
    Next i
    Set myRange = Nothing
End Sub
```

## 058 參照列和欄
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 列欄編號()
    Dim allRange As Range
    Set allRange = Range("A3").CurrentRegion
    
    allRange.Columns("A:B").HorizontalAlignment = xlCenter
    allRange.Rows(1).Select
    Selection.HorizontalAlignment = xlCenter
    Selection.Interior.ColorIndex = 38
    Set allRange = Nothing
End Sub

Sub 日期格式()
    Cells.ClearFormats
    Columns(1).NumberFormatLocal = "yyyy/mm/dd"
End Sub
```

## 059 參照儲存格範圍的整列和整欄
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 插入整個列()
    Dim i As Integer, rcnt As Integer
    
    rcnt = Cells(Rows.Count, 1).End(xlUp).Row
    For i = rcnt To 3 Step -1
        If Cells(i, 1).Value Like "*部" Then
           Cells(i, 1).EntireRow.Insert
           Cells(i, 1).EntireRow.Interior.ColorIndex = 0
        End If
    Next i
End Sub
```

## 060 儲存格範圍的列數和欄數
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 取得列數和欄數()
    Dim rcnt As Long, ccnt As Long
    
    rcnt = Rows.Count
    ccnt = Columns.Count
    MsgBox "工作表的列數: " & rcnt & Chr(10) & _
           "工作表的欄數: " & ccnt
    rcnt = Range("A3:C10").Rows.Count
    ccnt = Range("A3:C10").Columns.Count
    MsgBox "表的列數: " & rcnt & Chr(10) & _
           "表的欄數: " & ccnt
End Sub
```

## 061 為儲存格範圍的名稱
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 定義名稱()
    Range("A2").CurrentRegion.Name = "年間目標"
    Range("年間目標").Select
End Sub

Sub 定義名稱2()
    ActiveWorkbook.Names.Add Name:="年間目標", RefersTo:=Range("A2").CurrentRegion
    Range("年間目標").Select
End Sub
```

## 062 刪除儲存格範圍的名稱
```
Attribute VB_Name = "Module2"
Option Explicit

Sub 刪除名稱()
    On Error GoTo errHandler
    ActiveWorkbook.Names("年間目標").Delete
    Exit Sub
errHandler:
    MsgBox "沒有名稱!!"
End Sub

Sub 刪除全部名稱()
    Dim xName As Name
    For Each xName In ActiveWorkbook.Names
        xName.Delete
    Next
End Sub
```

## 063 取得特定種類的儲存格
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 參照空白儲存格()
    Dim myRange As Range
    Set myRange = Range("A2").CurrentRegion.SpecialCells(xlCellTypeBlanks)
    myRange.Value = 0
    MsgBox "空白儲存格: " & myRange.Address(False, False, xlA1)
    Set myRange = Nothing
End Sub

Sub 將儲存格轉為空白()
    
    Range("A2").CurrentRegion.Replace _
            What:=0, Replacement:="", Lookat:=xlWhole
End Sub
```

## 064 跳到指定的儲存格
```
Attribute VB_Name = "Module2"
Option Explicit

Sub CellJump()
    Application.Goto Range("A8:F12")
End Sub

Sub CellJump2()
    Application.Goto Reference:=Worksheets("5ды").Range("A8"), Scroll:=True
End Sub
```

## 065 參照到目前使用的字型
```
Attribute VB_Name = "Module2"
Option Explicit

Sub 參照字型()
    With Range("B1:C6").Font
        .Name = "MS PGothic"
        .FontStyle = "斜體"
        .Size = 16
        .Color = RGB(0, 115, 190)
        .TintAndShade = 0
        .ThemeFont = xlThemeFontNone
    End With
End Sub
```

## 066 改變字型的大小和種類
```
Attribute VB_Name = "Module2"
Option Explicit

Sub 設定字型的尺寸種類()
    With Range("B1").Font
        .Name = "MS UI Gothic"
        .Size = 18
    End With
    With Range("B3:C6").Font
        .Name = Application.StandardFont
        .Size = Application.StandardFontSize
    End With
End Sub
```

## 067 塗滿儲存格
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 塗滿儲存格()
    Dim myRange As Range
    For Each myRange In Range("C4:D10")
        If myRange.Value = "陳" Then
           myRange.Interior.Color = RGB(150, 255, 100)
        End If
    Next
End Sub

Sub 設定儲存格的顏色()
    Range("C4:D10").Font.ColorIndex = xlColorIndexAutomatic
    Range("C4:D10").Interior.ColorIndex = xlColorIndexNone
End Sub

Sub 設定儲存格的布景主題()
    Range("A1").Interior.ThemeColor = xlThemeColorAccent6
End Sub
```

## 068 變更字型樣式
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 設定字型樣式()
    Range("C1").Font.Bold = True
    Range("D2").Font.Italic = True
    Range("C4").Font.Underline = xlUnderlineStyleDoubleAccounting
End Sub
```

## 069 設定顏色的濃淡
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 顏色濃淡()
    Dim myRange As Range, i As Integer
    Set myRange = Range("A4:C10")
    For i = 1 To myRange.Rows.Count
        myRange.Rows(i).Interior.ThemeColor = xlThemeColorAccent6
        Select Case myRange.Cells(i, 3).Value
            Case 10: myRange.Rows(i).Interior.TintAndShade = 0.8
            Case 20: myRange.Rows(i).Interior.TintAndShade = 0.6
            Case 30: myRange.Rows(i).Interior.TintAndShade = 0
            Case 40: myRange.Rows(i).Interior.TintAndShade = -0.25
        End Select
    Next i
    Set myRange = Nothing
End Sub
```

## 070 設定儲存格的網底圖樣
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 設定網底()
    With Range("A3:G3,A7")
        .Interior.Pattern = xlPatternGray75
        .Interior.PatternColor = RGB(255, 150, 200)
    End With
End Sub
```

## 071 設定儲存格的線性漸層顏色
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 傾斜()
    With Range("B4:B6").Interior
        .Pattern = xlPatternLinearGradient
        .Gradient.Degree = 0
        .Gradient.ColorStops.Clear
        .Gradient.ColorStops.Add(0).Color = RGB(255, 255, 255)
        .Gradient.ColorStops.Add(1).Color = Range("B3").Interior.Color
    End With
End Sub
```

## 072 繪製儲存格框線
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 設定框線()
    With Range("B3:C6")
        .Borders(xlEdgeTop).LineStyle = xlContinuous
        .Borders(xlEdgeBottom).LineStyle = xlContinuous
        .Borders(xlEdgeLeft).LineStyle = xlContinuous
        .Borders(xlEdgeRight).LineStyle = xlContinuous
        .Borders(xlInsideVertical).LineStyle = xlDouble
        .Borders(xlInsideHorizontal).LineStyle = xlDash
    End With
End Sub

Sub 設定框線2()
    Range("B3:C6").Borders.LineStyle = xlContinuous
End Sub

Sub 設定框線3()
    Range("B3:C6").Borders.LineStyle = xlNone
End Sub

Sub 設定框線4()
    Range("B3:C6").BorderAround LineStyle:=xlContinuous
End Sub
```

## 073 指定框線種類
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 設定各種框線()
    Range("B3:C6").BorderAround LineStyle:=xlContinuous, Weight:=xlThick
    Range("B3:C3").Borders(xlEdgeBottom).LineStyle = xlDouble
    Range("B3:C3").Borders(xlEdgeBottom).Weight = xlMedium
End Sub
```

## 074
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 設定儲存格樣式()
    Range("B1").Style = "標題"
    Range("B3:C3").Style = "60% - 輔色3"
    Range("B4:B9").Style = "40% - 輔色3"
    Range("C4:C9").Style = "20% - 輔色3"
    Range("C9").Style = "百分比"
End Sub
```

## 075 登錄使用者定義的儲存格樣式
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 新增樣式()
    On Error GoTo errHandler
    With ActiveWorkbook.Styles.Add(Name:="myTitle")
         .Interior.ColorIndex = 38
         .HorizontalAlignment = xlHAlignCenter
         .Font.Size = 16
         .Font.ColorIndex = 56
    End With
    Range("B1:C1").Style = "myTitle"
    Exit Sub
errHandler:
   MsgBox "樣式名重複!!"
End Sub

Sub 刪除樣式()
    On Error Resume Next
    ActiveWorkbook.Styles("myTitle").Delete
End Sub
```

## 076 設定儲存格的格式
```
Attribute VB_Name = "Module2"
Option Explicit

Sub 設定格式()
   Range("A5:A9").NumberFormatLocal = "mm/dd"
   Range("B5:B9").NumberFormatLocal = "h:mm AM/PM"
   Range("C3").NumberFormatLocal = """受理:""@"
   Range("A13:C13").NumberFormatLocal = "#,##0;[紅色]-#,##0"
End Sub


Sub 設定格式2()
    Cells.NumberFormat = "general"
    Range("A4:A9").NumberFormatLocal = "yyyy/m/d"
    Range("B5:B9").NumberFormatLocal = "h:mm"
End Sub
```

## 077 刪除儲存格的值或格式
```
Attribute VB_Name = "Module1"
Option Explicit

Sub Clear()
    Range("A1:D1").ClearFormats
    Range("A5:C7").ClearContents
    Range("D3").ClearComments
    Range("A10:D10").Clear
End Sub
```

## 078 依欄寬自動換列
```
Attribute VB_Name = "Module2"
Option Explicit

Sub 換行切換()
    With Range("C4:C7")
         .WrapText = Not .WrapText
    End With
End Sub

Sub 換行切換2()
Attribute 換行切換2.VB_ProcData.VB_Invoke_Func = " \n14"
    
    Range("C4:C7").WrapText = True
            
End Sub

Sub 換行切換3()
    
    Range("C4:C7").WrapText = False

End Sub

Sub 換行切換4()
    
    Range("C4:C7").ShrinkToFit = True
        
End Sub

Sub 換行切換5()
    
    Range("C4:C7").ShrinkToFit = False
        
End Sub

Sub 分割字串()
    Range("A1").Justify
End Sub
```

## 079 參照合併儲存格
```
Attribute VB_Name = "Module2"
Option Explicit

Sub 參照合併儲存格()
    Range("A3").Value = 2006
    Range("A4").Value = "12月"
    Range("A5").MergeArea.Value = 2007
    Range("A6").MergeArea.Value = "1月"
    Range("B3").MergeArea.ClearContents
    Range("B5").ClearContents
End Sub
```

## 080 合併儲存格
```
Attribute VB_Name = "Module2"
Option Explicit

Sub 合併儲存格()
    Application.DisplayAlerts = False
    Range("A4:A9").Merge
    Range("B10:D13").Merge Across:=True
    Application.DisplayAlerts = True
End Sub
```

## 081 設定條件化格式
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 設定條件化格式()
    Dim myRange As Range, myFC As FormatCondition
    Set myRange = Worksheets(1).Range("E3:E14")
    If myRange.FormatConditions.Count > 0 Then myRange.FormatConditions.Delete
    Set myFC = myRange.FormatConditions.Add _
        (Type:=xlCellValue, Operator:=xlGreater, Formula1:="=1")
    myFC.Font.Bold = True
    myFC.Interior.Color = RGB(140, 180, 230)
    Set myRange = Nothing: Set myFC = Nothing
End Sub

Sub 設定條件化格式為資料橫條()
    Dim myRange As Range, myDB As DataBar
    Set myRange = Worksheets(1).Range("E3:E14")
    Set myDB = myRange.FormatConditions.Add(Type:=xlDatabar)
    myDB.BarColor.Color = RGB(150, 255, 100)
    Set myRange = Nothing: Set myDB = Nothing
End Sub
```

## 082 確認是否為合併的儲存格
```
Attribute VB_Name = "Module2"
Option Explicit

Sub 儲存格的合併和刪除()
    Dim i As Integer
    
    If Range("A3").MergeCells = True Then
       Range("A3").MergeCells = False
    End If
    Application.DisplayAlerts = False
    For i = 3 To 13 Step 2
        Range(Cells(i, 2), Cells(i, 2).Offset(1)).MergeCells = True
    Next i
    Application.DisplayAlerts = True
End Sub
```

## 083 設定超連結
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 新增超連結()
    ActiveSheet.Hyperlinks.Add anchor:=Range("B3"), _
    Address:="c:\月間銷售\1月.xls", SubAddress:="1月!A1", TextToDisplay:="1月"
End Sub

Sub 刪除超連結()
    Dim myHyperLink As Hyperlink
    
    For Each myHyperLink In ActiveSheet.Hyperlinks
        myHyperLink.Delete
    Next
End Sub

Sub 新增超連結2()
    ActiveSheet.Hyperlinks.Add anchor:=Range("D3"), _
        Address:="http://www.flag.com.tw/", _
        TextToDisplay:="旗標資訊"
End Sub
Sub 新增超連結3()
    ActiveSheet.Hyperlinks.Add anchor:=Range("B3"), _
        Address:="", _
        SubAddress:="Sheet2!A1", _
        TextToDisplay:=Worksheets(2).Name
End Sub
```

## 084 插入註解
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 新增註解()
    On Error GoTo errHandler
    Range("A1").AddComment "做成者: " & Application.UserName
    With Range("A10")
        .AddComment "輸入日: " & Chr(10) & Date
        .Comment.Shape.AutoShapeType = msoShape24pointStar
        .Comment.Visible = True
    End With
    Exit Sub
errHandler:
    MsgBox "完成插入註解!!"
End Sub
```

## 085 將列或欄隱藏
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 列欄的顯示隱藏()
    
    Rows("4:9").Hidden = Not Rows("4:9").Hidden
    Columns("B:C").Hidden = Not Columns("B:C").Hidden
    
End Sub
```

## 086 設定或取得儲存格的值
```
Attribute VB_Name = "Module2"
Option Explicit

Sub 設定或取得值()
    Range("A5").Value = #5/7/2007#
    Range("B5").Value = #10:30:00 AM#
    Range("C5").Value = "王大董"
    
    Range("A13").Value = Range("B13")
    Range("B13").Value = 16
End Sub
```

## 087 取得在儲存格上顯示的值
```
Attribute VB_Name = "Module2"
Option Explicit

Sub 取得值()
    
    MsgBox "使用Value屬性取得: " & Range("A5").Value & Chr(10) & _
           "使用Text屬性取得: " & Range("A5").Text
End Sub
```

## 088 設定或取得儲存格的公式
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 計算式()
    Range("D4").Formula = "=B4*C4"
    Range("D5:D6").FormulaR1C1 = Range("D4").FormulaR1C1

    Range("C7").Formula = "=SUM(C4:C6)"
    Range("D7").FormulaR1C1 = Range("C7").FormulaR1C1
End Sub

Sub 計算式2()
    Range("D4:D6").FormulaR1C1 = "=RC[-2]*RC[-1]"
    Range("C7:D7").FormulaR1C1 = "=SUM(R[-3]C:R[-1]C)"
End Sub
```

## 089 取得儲存格的高度和寬度
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 儲存格的寬和高()
    Range("A1").Select
    MsgBox "儲存格A1的高度: " & Selection.Height & "點" & Chr(10) & _
           "儲存格A1的寬度: " & Selection.Width & "點"
    ActiveSheet.UsedRange.Select
    MsgBox "使用儲存格範圍的高度: " & Selection.Height & "點" & Chr(10) & _
           "使用儲存格範圍的寬度: " & Selection.Width & "點"
End Sub

Sub 已用範圍的寬和高()
    ActiveSheet.UsedRange.Select
    MsgBox "使用儲存格範圍的高度: " & Selection.Height * 0.035 & "cm" & Chr(10) & _
           "使用儲存格範圍的寬度: " & Selection.Width * 0.035 & "cm"
End Sub
```

## 090 變更列高和欄寬
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 調整列高欄寬()
    Range("A1").RowHeight = 30
    Range("A3").CurrentRegion.RowHeight = 20
    Range("A3").CurrentRegion.ColumnWidth = 12
End Sub

Sub 調整列高欄寬2()
    Rows(1).RowHeight = 30
    Rows("3:8").RowHeight = 20
    Columns("B:E").ColumnWidth = 12
End Sub
```

## 091 恢復為標準列高和欄寬
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 恢復標準列高欄寬()
    Range("A3").CurrentRegion.UseStandardHeight = True
    Range("A3").CurrentRegion.UseStandardWidth = True
End Sub

Sub 整個工作表恢復標準列高和欄寬()
    Rows.RowHeight = ActiveSheet.StandardHeight
    Columns.ColumnWidth = ActiveSheet.StandardWidth
End Sub
```

## 092 自動調整列高欄寬
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 自動調整列高欄寬()
    Rows(1).AutoFit
    Range("A3").CurrentRegion.Rows.AutoFit
    Range("A3").CurrentRegion.Columns.AutoFit
End Sub
```

## 093 變更儲存格的文字對齊方式
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 變更對齊方式()
    Range("A1:B1").HorizontalAlignment = xlCenterAcrossSelection
    Range("A3:B6").HorizontalAlignment = xlCenter
    Range("A3:B6").VerticalAlignment = xlCenter
End Sub

Sub 變更對齊方式2()
    Range("A3:B3").Orientation = 30
    Range("A4:A6").Orientation = xlVertical
End Sub
```

## 094 新增儲存格
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 插入儲存格()
    Dim i As Integer, xRange As Range
    Set xRange = Range("A4").CurrentRegion
    xRange.Columns(3).Insert xlToRight, xlFormatFromLeftOrAbove
    xRange.Columns(3).FormulaR1C1 = "=if(RC[4]>=180, ""合格"",""不合格"")"
    xRange.Cells(1, 3) = "是否合格"
    Set xRange = Nothing
End Sub
```

## 095 刪除儲存格
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 儲存格插入刪除()
    Dim i As Integer, xRange As Range
    Set xRange = Range("A4").CurrentRegion
    For i = xRange.Rows.Count To 2 Step -1
        If xRange.Cells(i, 7) = 0 Then
           xRange.Rows(i).Delete shift:=xlShiftUp
        End If
    Next
    Set xRange = Nothing
End Sub

Sub 列欄插入刪除()
    Range("A3").EntireRow.Insert
    Range("A3").EntireColumn.Delete
End Sub
```

## 096 將儲存格的值複製到其他儲存格
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 複製()
    Range("A5:F9").Copy
    ActiveSheet.Paste Range("A13")
    ActiveSheet.Paste Range("A21")
    Application.CutCopyMode = False
End Sub
```

## 097 不經過剪貼簿直接複製並貼上
```
Attribute VB_Name = "Module2"
Option Explicit

Sub 直接複製()
    Range("A5:F9").Copy Destination:=Range("A13")
End Sub

Sub 移動()
    Range("A5:D9").Cut Destination:=Range("A13")
End Sub
```

## 098 選擇格式後貼上
```
Attribute VB_Name = "Module2"
Option Explicit

Sub 選擇格式貼上()
    Range("A5:D9").Copy
    Range("F5").PasteSpecial Paste:=xlPasteColumnWidths
    Range("F5").PasteSpecial Paste:=xlPasteFormats
    Application.CutCopyMode = False
End Sub
```