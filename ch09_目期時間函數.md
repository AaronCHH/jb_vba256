# Ch09 日期時間函數
## 185 取得現在的日期和時間
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 顯示現在的日期和時間()
    MsgBox "現在的日期和時間" & Chr(10) & _
           "日期: " & Date & Chr(10) & "時間: " & Time, _
           , "確認日期和時間: " & Now
End Sub
```
## 186 取得現在年月日
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 個別取得年月日()
    MsgBox "西曆: " & Year(Range("B1")) & Chr(10) & _
           "  月: " & Month("民國83年11月5日") & Chr(10) & _
           "  日: " & Day(#11/5/1994#)
End Sub

Sub 指定年月日測試()
    MsgBox Year("民國98-09-01") & Chr(10) & _
            Month("2009/09/01") & Chr(10) & _
            Day("２００９年９月１日")
End Sub
```
## 187 取得現在時分秒
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 個別取得時分秒()
    MsgBox "現在的時間" & Chr(10) & _
            Hour(Now) & "時" & Chr(10) & _
            Minute(Now) & "分" & Chr(10) & _
            Second(Now) & "秒"
End Sub

Sub 指定時分秒測試()
    MsgBox Hour("8時35分 PM") & Chr(10) & _
            Minute("20:35") & Chr(10) & _
            Second("下午8時35分")
End Sub
```
## 188 從日期時間的資料取得指定單位的值
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 取得時間單位()
    Dim dp As Integer
    dp = DatePart("y", Date)
    MsgBox "今天的日期: " & Date & Chr(10) & _
           "1月1日到現在的經過日數: " & dp & Chr(10) & _
           "1年當中經過了: " & Format(dp / 365, "0.0%")
End Sub
```
## 189 取得星期
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 取得星期()
    MsgBox "您的生日: " & Range("B1") & Chr(10) & _
           "您的出生日期: " & _
            WeekdayName(Weekday(Range("B1")))
End Sub

Sub 取得星期2()
    MsgBox "您的生日: " & Range("B1") & Chr(10) & _
           "您的出生日期: " & _
            Format(Range("B1"), "dddd")
End Sub
```
## 190 取得 2 個日期時間的間隔
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 取得經過日期時間()
    Dim sDate As Date
    sDate = #11/5/1994#
    MsgBox "出生年月日: " & sDate & Chr(10) & _
           "今天的日期: " & Date & Chr(10) & _
           "經過年數: " & DateDiff("yyyy", sDate, Date)
End Sub
```
## 191 取得經過指定時間後的期時間
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 日期時間加算()
    Dim i As Integer
    For i = 0 To 11
        Cells(i + 2, 2).Value = DateAdd("ww", 4 * i, Date)
    Next
End Sub
```
## 192 將字串轉為日期或時間
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 日期時間轉換()
    Dim strDate As String, strTime As String
    strDate = "民國98年9月1日"
    strTime = "下午3時25分"
    MsgBox "日期: " & DateValue(strDate) & Chr(10) & _
           "時間: " & TimeValue(strTime) & Chr(10) & _
           "1週後: " & DateValue(strDate) + 7 & Chr(10) & _
           "2小時後: " & TimeValue(strTime) + 2 / 24
End Sub
```
## 193 用日期的各元素組合成日期資料
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 做成日期()
    Dim myDate As Date, myDate2 As Date
    myDate = DateSerial(Range("A2"), Range("A3"), Range("A4"))
    myDate2 = DateSerial(Year(Date), Range("A3"), Range("A4"))
    MsgBox "您的誕生日: " & myDate & Chr(10) & _
           "今年的誕生日是 " & myDate2 & Format(myDate2, "(aaaa)") _
           & " 沒錯吧!!"
End Sub
```
## 194 將時間的各元素組合成時間資料
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 組合成時間()
    Dim myTime As Date, myTime2 As Date, myTime3 As Date
    myTime = TimeSerial(Range("A2"), Range("A3"), 0)
    myTime2 = TimeSerial(Range("A2"), Range("A3") - 10, 0)
    myTime3 = TimeSerial(Range("A2") + 1, Range("A3"), 0)
    MsgBox "預約時間: " & myTime & Chr(10) & Chr(10) & _
           "請於 " & myTime2 & " 前到達!! " & Chr(10) & _
           "超過 " & myTime3 & " 則無效!! "
End Sub
```
## 195 取得經過的秒數
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 回答測定時間()
    Dim s1 As Single, s2 As Double, qText As String, aText As String
    Dim ansText As String, msgText As String
    
    qText = "英國的正式名稱是?? (半形英文字母)"
    aText = "UNITED KINGDOM"
    s1 = Timer
    ansText = InputBox(qText, "問題")
    s2 = Timer - s1
    msgText = "正確答案: " & aText & Chr(10) & "回答時間: " & s2 & "秒"
    If StrComp(aText, ansText, 1) = 0 Then
       MsgBox "正確!!" & Chr(10) & msgText
    Else
       MsgBox "錯誤!!" & Chr(10) & msgText
    End If
End Sub
```
