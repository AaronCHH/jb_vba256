# Ch04 工作表
## 112 參照工作表
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 參照工作表()
    Dim i As Integer
    With Worksheets("Sheet1")
        .Range("B1").Value = Sheets.Count
        .Range("B2").Value = Worksheets.Count
        .Range("B3").Value = Charts.Count
    End With
    MsgBox ActiveSheet.Name
End Sub
```

## 113 選取工作表
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 選擇工作表()
    Dim i As Integer, myName As Integer
    Dim mySheet As Worksheet
    Worksheets(2).Activate
    MsgBox "選擇的工作表: " & ActiveSheet.Name
    Worksheets(1).Select
    Worksheets(3).Select Replace:=False
End Sub
```

## 114 計算工作表的個數
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 工作表數()
    MsgBox "工作表數: " & Worksheets.Count
    Worksheets(Array(1, 3)).Select
    MsgBox "選擇的工作表數: " & ActiveWindow.SelectedSheets.Count
End Sub
```

## 115 新增工作表
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 新增工作表()
    Dim i As Integer
    
    Do Until Worksheets.Count = 12
        i = Worksheets.Count
        Worksheets.Add After:=Worksheets(i)
        ActiveSheet.Name = i + 1 & "月"
    Loop
End Sub
```

## 116 刪除工作表
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 刪除工作表()
    Dim myWS As Worksheet
    Application.DisplayAlerts = False
    For Each myWS In Worksheets
        If myWS.Name Like "2006*" Then
           myWS.Delete
        End If
    Next
    Application.DisplayAlerts = True
End Sub
```

## 117 複製工作表
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 複製工作表()
    Dim myMonth, myYear
    myMonth = Right(Worksheets(1).Name, 2)
    myYear = Left(Worksheets(1).Name, 4)
    
    Worksheets("Template").Copy Before:=Worksheets(1)
    If myMonth = 12 Then
       ActiveSheet.Name = myYear + 1 & "-01"
    Else
       ActiveSheet.Name = myYear & "-" & Format(myMonth + 1, "00")
    End If
End Sub
```

## 118 移動工作表
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 移動工作表()
    Dim myWS As Worksheet
    For Each myWS In Worksheets
        If Left(myWS.Name, 4) = "2006" Then
           myWS.Move before:=Workbooks("2006年.xls").Worksheets(1)
        End If
    Next
End Sub
```

## 119 切換工作表的顯示或隱藏
```
Attribute VB_Name = "Module1"

Sub 工作表的顯示或隱藏()

    With Worksheets("Template")
      If .Visible = True Then
         .Visible = xlSheetVeryHidden
      Else
         .Visible = True
      End If
    End With
End Sub
```

## 120 設定或取得工作表名
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 確認工作表名()
    Dim myWS As Worksheet, myName As String
    
    myName = Format(Date, "yyyy-mm")
    For Each myWS In Worksheets
        If myWS.Name = myName Then
           MsgBox "同名的工作表存在!!"
           Exit Sub
        End If
    Next
    Worksheets("Template").Copy Before:=Worksheets(1)
    ActiveSheet.Name = myName
End Sub
```

## 121 改變工作表的索引標籤顏色
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 改變工作表的索引標籤顏色()
    Dim mySheet As Worksheet
    For Each mySheet In Worksheets
        Select Case Left(mySheet.Name, 4)
            Case "2006"
                mySheet.Tab.Color = RGB(80, 255, 255)
            Case "2007"
                mySheet.Tab.Color = RGB(255, 255, 80)
        End Select
    Next
End Sub
```

## 122 限制游標在工作表內移動的範圍
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 指定儲存格移動範圍()
    Worksheets("英語測試").ScrollArea = Range("A3").CurrentRegion.Address
End Sub

Sub 解除儲存格移動範圍()
    Worksheets("英語測試").ScrollArea = ""
End Sub
```

## 123 設定與解除工作表的保護
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 保護工作表()
    On Error Resume Next
    Range("C4:D13").Locked = False
    ActiveSheet.Protect Password:="hogo"
End Sub

Sub 保護工作表解除()
    On Error GoTo errHander
    ActiveSheet.Unprotect
    Exit Sub
errHander:
    MsgBox "請輸入正確密碼!!!"
End Sub
```

## 124 改變工作表的順序
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 工作表的排序()
    Dim i As Integer, sRange As Range, sName As String
    Worksheets.Add(before:=Worksheets(1)).Name = "temp"
    For i = 2 To Worksheets.Count
        Cells(i, 1) = Worksheets(i).Name
    Next
    Set sRange = Cells(2, 1).CurrentRegion
    
    With ActiveSheet.Sort
        .SortFields.Clear
        .SortFields.Add Key:=Range("A2"), SortOn:=xlSortOnValues, _
                        Order:=xlAscending
        .SetRange sRange
        .Apply
    End With
    For i = 1 To sRange.Rows.Count
        sName = Worksheets("temp").Cells(i + 1, 1)
        Worksheets(sName).Move after:=ActiveSheet
    Next
    Application.DisplayAlerts = False
    Worksheets("temp").Delete
    Application.DisplayAlerts = True
End Sub
```

## 125 工作表中資料更新時自動執行的程序
```
VERSION 1.0 CLASS
BEGIN
  MultiUse = -1  'True
END
Attribute VB_Name = "Sheet1"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = True
Option Explicit

Private Sub Worksheet_Change(ByVal Target As Range)
    On Error GoTo errhandler
    If Target.Column <> 2 Then Exit Sub
    If Target.Offset(, -1) <> "" Then
       Target.Offset(, 1) = Date
    End If
    Exit Sub
errhandler:
End Sub

Private Sub Worksheet_Activate()
    Dim ans As Integer
    ans = MsgBox("可以刪除作業內容和作業日嗎!!", _
        vbYesNo, "刪除確認")
    If ans = vbYes Then
        Range("B4:C7").ClearContents
    End If
End Sub
```

## 126 確認工作表是否處於保護狀態
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 保護工作表確認()
    If ActiveSheet.ProtectContents Then
        MsgBox "工作表處於保護狀態!!"
        Exit Sub
    End If
    Range("C4:D13").Locked = False
    ActiveSheet.Protect Password:="hogo"
End Sub
```
