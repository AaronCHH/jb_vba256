# Ch12 圖表、圖案
## 216 新增圖表工作表 
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 插入圖表()
    With Charts.Add(after:=ActiveSheet)
        .Name = "綜合G"
        .SetSourceData Sheets("綜合").Range("B3:E13")
    End With
End Sub
```
## 217 指定圖表區的資料範圍
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 圖表範圍變更()
    Dim sd As Variant, gRange As Range
    Charts("圖表").Activate
    sd = InputBox("指定圖表化的科目:  國語: 1, 英語 2, 數學 3")
    Select Case sd
        Case 1: Set gRange = Worksheets("3教科").Range("B3:D13")
        Case 2: Set gRange = Worksheets("3教科").Range("B19:D29")
        Case 3: Set gRange = Worksheets("3教科").Range("B35:E45")
        Case Else
            MsgBox "指定不正確!!"
            Exit Sub
    End Select
    Charts("圖表").SetSourceData gRange
    Set gRange = Nothing
End Sub
```
## 218 建立崁入圖表
```
Attribute VB_Name = "Module1"
Sub 建立嵌入圖表()
    Dim gr As Range
    Set gr = Worksheets("3 教科").Range("H1:M15")
    With ActiveSheet.ChartObjects.Add( _
        gr.Left, gr.Top, gr.Width, gr.Height)
        .Name = "3 教科 G"
        .Chart.SetSourceData Range("B3:E13")
    End With
    Set gr = Nothing
End Sub
```
## 219 變更圖表的種類 
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 變更圖表種類()
    ActiveSheet.ChartObjects("3教科G").Chart.ChartType = xl3DBarClustered
End Sub
```
## 220 設定圖表的標題
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 設定圖表的標題()
    Dim a As String
    With ActiveSheet.ChartObjects("3教科G").Chart
        .HasTitle = True
        .ChartTitle.Text = Worksheets("綜合").Range("A1")
        .ChartTitle.Font.Size = 18
    End With
End Sub
```
## 221 設定座標軸標題
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 設定軸標籤()
    ActiveSheet.ChartObjects("數學G").Select
    With ActiveChart.Axes(Type:=xlValue)
        .HasTitle = True
        .AxisTitle.Text = "分數"
        .AxisTitle.Orientation = xlVertical
    End With
End Sub
```
## 222 設定座標軸刻度標籤的格式
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 設定軸刻度標籤()
    ActiveSheet.ChartObjects("數學G").Select
    ActiveChart.Axes(Type:=xlValue).TickLabels.NumberFormat = "0分"
    ActiveChart.Axes(Type:=xlCategory).TickLabels.Orientation = xlVertical
End Sub

Sub 設定版面配置()
    With ActiveChart
        .ApplyLayout (5)
        .HasTitle = False
        .Axes(Type:=xlValue).AxisTitle.Text = "版面配置 1"
    End With
End Sub

Sub 設定圖表的樣式()
    ActiveChart.ChartStyle = 29
End Sub
```
## 223 設定圖例
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 設定圖例()
    ActiveSheet.ChartObjects("販賣G").Select
    With ActiveChart
        .HasLegend = True
        .Legend.Position = xlLegendPositionBottom
        .Legend.Font.Size = 12
    End With
End Sub

Sub 做成崁入圖表()
    Dim gr As Range
    Set gr = Worksheets("販賣業績").Range("H1:M15")
    With ActiveSheet.ChartObjects.Add(gr.Left, gr.Top, gr.Width, gr.Height)
        .Name = "販賣G"
        .Chart.SetSourceData Range("A1:E5")
    End With
    Set gr = Nothing
End Sub

Sub 設定圖表中的各元素()
    ActiveChart.SetElement (msoElementLegendBottom)
End Sub
```
## 224 將崁入圖表移動到圖表工作表
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 將崁入圖表移動到圖表工作表()
    ActiveSheet.ChartObjects("1").Select
    ActiveChart.Location Where:=xlLocationAsNewSheet, Name:="業績圖表"
End Sub

Sub 將崁入圖表移動到其他工作表()
    ActiveChart.Location Where:=xlLocationAsObject, Name:="Sheet2"
End Sub
```
## 225 建立圖案
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 將圖插入最大值的儲存格()
    Dim max As Long, r As Range
    max = Application.WorksheetFunction.max(Range("B2:E5"))
    Set r = Range("B2:E5").Find(max)
    With ActiveSheet.Shapes.AddShape _
            (msoShapeOval, r.Left, r.Top, r.Width, r.Height)
        .Name = "MAX"
        .Fill.Visible = msoFalse
        .Line.ForeColor.RGB = RGB(150, 205, 0)
    End With
End Sub

Sub 設定圖形輪廓()
    ActiveSheet.Shapes(1).Line.ForeColor.RGB = RGB(255, 0, 0)
    ActiveSheet.Shapes(1).Line.Weight = 4
End Sub

Sub 設定圖形填色()
    ActiveSheet.Shapes(1).Fill.ForeColor.RGB = RGB(255, 255, 0)
    ActiveSheet.Shapes(2).Fill.PresetTextured msoTextureBouquet
    ActiveSheet.Shapes(3).Fill.OneColorGradient msoGradientFromCenter, 2, 1
End Sub
```
## 226 參照圖案
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 參照圖形()
    Dim i As Integer
    For i = 1 To ActiveSheet.Shapes.Count
        ActiveSheet.Shapes(i).Select
        Selection.Text = i & ": " & Selection.Name
    Next
End Sub
```
## 227 建立文字方塊
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 新增文字方塊()
    Dim r As Range
    Set r = Range("A8:F9")
    With ActiveSheet.Shapes.AddTextbox(msoTextOrientationHorizontal, _
        r.Left, r.Top, r.Width, r.Height)
        .Name = "Text1"
        .TextFrame.Characters.Text = "HDD錄影機業績佳!!!"
    End With
    
End Sub
```
## 228 彙總圖案一起操作
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 設定複數圖形的格式()
    ActiveSheet.Shapes.SelectAll
    Selection.ShapeRange.ShapeStyle = msoShapeStylePreset10
    ActiveSheet.Shapes.Range(Array("橢圓 3", "七角星形 2")).Fill.Visible = False
End Sub
```
## 229 將崁入圖表以外的圖案刪除
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 刪除圖表以外的圖形()
    Dim myShape As Shape
    For Each myShape In ActiveSheet.Shapes
        If myShape.HasChart = msoFalse Then
           myShape.Delete
        End If
    Next
End Sub
```