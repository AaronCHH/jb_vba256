# Ch14 外部資料操作與其他
## 246 打開以逗號分隔欄位的文字檔
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 打開以逗號分隔的文字檔()
    Workbooks.OpenText Filename:="textdata.txt", Startrow:=2, _
                       DataType:=xlDelimited, Comma:=True
End Sub

Sub 以指定資料格式打開()
    Workbooks.OpenText Filename:="textdata.txt", Comma:=True, _
        Fieldinfo:=Array(Array(1, 2), Array(2, 1), Array(3, 9), Array(4, 3))
End Sub
```
## 247 以列為單位讀入文字檔
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 打開文字檔()
    Dim myFso As New FileSystemObject, myText As TextStream
    Dim i As Integer, j As Integer, rLine As String, myArray() As String
   
    Set myText = myFso.OpenTextFile(Filename:="textdata1.txt", IOMode:=ForReading)
    i = 0
    Do Until myText.AtEndOfStream
        rLine = myText.ReadLine
        myArray = Split(rLine, ",")
        For j = 0 To UBound(myArray)
            Cells(i + 1, j + 1).Value = myArray(j)
        Next j
        i = i + 1
    Loop
    myText.Close
End Sub
```
## 248 以列為單位寫入文字檔
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 寫入文字檔()
    Dim myFso As New FileSystemObject, myText As TextStream
    Dim i As Integer, j As Integer, wLine As String
    Dim myArray() As Variant
    
    Set myText = myFso.OpenTextFile(Filename:="textdata.txt", _
    IOMode:=ForWriting, Create:=True)
    ReDim myArray(Range("A1").CurrentRegion.Columns.Count - 1)
    For i = 1 To Range("A1").CurrentRegion.Rows.Count
        For j = 0 To UBound(myArray)
            myArray(j) = Cells(i, j + 1).Text
            Next
        wLine = Join(myArray, ",")
        myText.WriteLine wLine
    Next
    myText.Close
End Sub

Sub 寫入文字檔2()
    Dim myFso As New FileSystemObject, myText As TextStream
    Dim i As Integer, j As Integer, wLine As String
    Dim myArray() As Variant
    
    Set myText = myFso.OpenTextFile(Filename:="textdata.txt", _
    IOMode:=ForAppending)
    ReDim myArray(Range("A1").CurrentRegion.Columns.Count - 1)
    For i = 1 To Range("A1").CurrentRegion.Rows.Count
        For j = 0 To UBound(myArray)
            myArray(j) = Cells(i, j + 1).Text
            Next
        wLine = Join(myArray, ",")
        myText.WriteLine wLine
    Next
    myText.Close
End Sub
```
## 249 操作其他的 Office 應用程式
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 連結Word()
    Dim myWord As New Word.Application
    Range("行程表").Copy
    With myWord
        .Visible = True
        .Documents.Open ThisWorkbook.Path & "\月行程表.doc"
        .Activate
        .Selection.MoveDown Unit:=wdLine, Count:=6
        .Selection.PasteExcelTable False, False, False
        .ActiveDocument.PrintOut
    End With
    Application.CutCopyMode = False
    Set myWord = Nothing
End Sub

Sub 連結Word2()
    Dim myWord As Object
    Set myWord = CreateObject("word.application")
    Range("行程表").Copy
    With myWord
        .Visible = True
        .Documents.Open ThisWorkbook.Path & "\月行程表.doc"
        .Activate
        .Selection.MoveDown Unit:=5, Count:=6
        .Selection.PasteExcelTable False, False, False
        .ActiveDocument.PrintOut
    End With
    Application.CutCopyMode = False
    Set myWord = Nothing
End Sub
```
## 250 啟動其他的應用程式並傳送按鍵
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 操作應用程式()
    Dim taskID As Double
    Dim kazu1 As Long, kazu2 As Long
    kazu1 = Range("A2").Value
    kazu2 = Range("C2").Value
    taskID = Shell("calc.exe", vbNormalFocus)
    SendKeys kazu1 & "{*}" & kazu2 & "{ENTER}", True
    SendKeys "^C", True
    Application.Wait Now + TimeValue("0:00:01")
    Range("E2").PasteSpecial xlPasteAll
End Sub


Sub 啟動記事本()
    Shell "notepad.exe ""textdata.txt""", vbNormalFocus
End Sub
```
## 251 複製　Access　資料庫的資料表
```
VERSION 1.0 CLASS
BEGIN
  MultiUse = -1  'True
END
Attribute VB_Name = "Sheet2"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = True
Option Explicit

Sub 複製資料表()
    Dim myCn As New ADODB.Connection
    Dim myRs As New ADODB.Recordset
    myCn.Provider = "Microsoft.Jet.OLEDB.4.0"
    myCn.Open ThisWorkbook.Path & "\客戶管理.mdb"
    myRs.Open "T_客戶", myCn
    Range("A2").CopyFromRecordset myRs
    myRs.Close: myCn.Close
    Set myRs = Nothing: Set myCn = Nothing
End Sub

Private Sub CommandButton1_Click()
    Call 複製資料表
End Sub
```
## 252 在 Access 的資料表中新增資料
```
VERSION 1.0 CLASS
BEGIN
  MultiUse = -1  'True
END
Attribute VB_Name = "Sheet1"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = True
Option Explicit

Sub 追加資料()
    Dim myCn As New ADODB.Connection
    Dim myRs As New ADODB.Recordset
    myCn.Provider = "Microsoft.Jet.OLEDB.4.0"
    myCn.Open ThisWorkbook.Path & "\客戶管理.mdb"
    myRs.Open "T_客戶", myCn, adOpenDynamic, adLockOptimistic
    myRs.AddNew
    
    myRs!客戶編號 = Range("A2").Value
    myRs!客戶名稱 = Range("B2").Value
    myRs!負責人 = Range("C2").Value
    myRs!郵遞區號 = Range("D2").Value
    myRs!住址 = Range("B2").Value
    myRs!電話 = Range("F2").Value
    myRs.Update
    
    myRs.Close: myCn.Close
    Set myRs = Nothing: Set myCn = Nothing
End Sub

```
## 253 更新 Access 的資料
```
VERSION 1.0 CLASS
BEGIN
  MultiUse = -1  'True
END
Attribute VB_Name = "Sheet1"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = True
Option Explicit

Sub 修改資料()
    Dim myCn As New ADODB.Connection
    Dim myRs As New ADODB.Recordset
    Dim mySQL As String
    myCn.Provider = "Microsoft.Jet.OLEDB.4.0"
    myCn.Open ThisWorkbook.Path & "\客戶管理.mdb"
    mySQL = "SELECT * FROM T_客戶 WHERE 客戶編號= " & Range("A2").Value
    myRs.Open mySQL, myCn, adOpenDynamic, adLockOptimistic
    myRs!客戶名稱 = Range("B2").Value
    myRs!負責人 = Range("C2").Value
    myRs!郵遞區號 = Range("D2").Value
    myRs!住址 = Range("E2").Value
    myRs!電話 = Range("F2").Value
    myRs.Update
    myRs.Close: myCn.Close
    Set myRs = Nothing: Set myCn = Nothing
End Sub
```
## 254 刪除 Access 的資料
```
VERSION 1.0 CLASS
BEGIN
  MultiUse = -1  'True
END
Attribute VB_Name = "Sheet1"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = True
Option Explicit

Sub 刪除資料()
    Dim myCn As New ADODB.Connection
    Dim myRs As New ADODB.Recordset
    Dim mySQL As String
    myCn.Provider = "Microsoft.Jet.OLEDB.4.0"
    myCn.Open ThisWorkbook.Path & "\客戶管理.mdb"
    mySQL = "SELECT * FROM T_客戶 WHERE 客戶名稱 Like '%工務所'"
    myRs.Open mySQL, myCn, adOpenDynamic, adLockOptimistic
    If myRs.EOF Then
       MsgBox "沒有記錄!!"
    Else
       Do While myRs.EOF = False
           myRs.Delete
           myRs.MoveNext
       Loop
    End If
    myRs.Close: myCn.Close
    Set myRs = Nothing: Set myCn = Nothing
End Sub
```
## 255 將登錄的巨集以按鈕顯示在快速存取工具列上
```

```
## 256 結束 Excel
```
VERSION 1.0 CLASS
BEGIN
  MultiUse = -1  'True
END
Attribute VB_Name = "Sheet1"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = True
Option Explicit

Sub 結束Excel()
    Dim ans As Integer
    ans = MsgBox("結束可以嗎??", vbOKCancel, "結束確認")
    If ans = vbOK Then
       ActiveWorkbook.Save
       Application.Quit
    End If
End Sub
```