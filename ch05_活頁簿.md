# Ch05 活頁簿

## 127 參照活頁簿
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 參照活頁簿()
    Dim xBook As Variant, i As Integer
    xBook = Array("課題1.xls", "課題2.xls", "課題3.xls")
    For i = 0 To 2
        Workbooks.Open Filename:=xBook(i)
    Next
    MsgBox Workbooks(1).Name & ":" & Workbooks(2).Name & ":" & _
        Workbooks(3).Name & ":" & Workbooks(4).Name
End Sub
```

## 128 參照作用中的活頁簿
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 參照作用中的活頁簿()
    Dim xBook As Variant, i As Integer
    MsgBox "目前作用中的活頁簿: " & ActiveWorkbook.Name & Chr(10) & _
           "執行程式中的活頁簿: " & ThisWorkbook.Name
    Workbooks.Open Filename:="課題1.xls"
    MsgBox "目前作用中的活頁簿: " & ActiveWorkbook.Name & Chr(10) & _
           "執行程式中的活頁簿: " & ThisWorkbook.Name
End Sub
```

## 129 將活頁簿轉為作用中
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 作用活頁簿()
    Dim xBook As Variant, i As Integer
    xBook = Array("課題1.xls", "課題2.xls", "課題3.xls")
    For i = 0 To 2
        Workbooks.Open Filename:=xBook(i)
        Workbooks(1).Worksheets(1).Cells(i + 2, "B").Value = xBook(i)
    Next
    Workbooks(1).Activate
End Sub
```

## 130 建立新活頁簿
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 新增活頁簿()
    Dim ans As Integer
    ans = MsgBox("請問新活頁簿的工作表預設1張嗎???", vbYesNo)
    If ans = vbYes Then
       Application.SheetsInNewWorkbook = 1
    Else
       Application.SheetsInNewWorkbook = 3
    End If
    Workbooks.Add
End Sub
```

## 131 打開活頁簿
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 打開活頁簿()
    On Error GoTo errHandler
    Workbooks.Open Filename:="業績表.xls", ReadOnly:=True
    Exit Sub
errHandler:
    MsgBox "找不到指定的檔案!!!"
End Sub
```

## 132 顯示「開啟舊檔」對話方塊
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 對話方塊選擇活頁簿()
    With Application.FileDialog(msoFileDialogOpen)
        .FilterIndex = 2
        .AllowMultiSelect = True
        If .Show = -1 Then .Execute
    End With
End Sub
```

## 133 關閉活頁簿
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 更新存檔並關閉()
    ActiveWorkbook.Close SaveChanges:=True
End Sub

Sub 存檔為無巨集的活頁簿()
    Worksheets("英語測試").Copy
    ActiveWorkbook.Close SaveChanges:=True, _
                         Filename:="C:\ExcelVBA\成績.xlsx"
End Sub

Sub 關閉所有活頁簿()
    Workbooks.Close
End Sub
```

## 134 活頁簿更新存檔
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 作用活頁簿更新存檔()
    Workbooks.Open Filename:="C:\ExcelVBA\數學.xls"
    Range("G1").Value = Date
   'Workbooks(2).Worksheets(1).Range("G1").Value = Date
    ActiveWorkbook.Save
End Sub

Sub 活頁簿更新存檔()
    Dim xBook As Workbook
    For Each xBook In Workbooks
        xBook.Save
    Next
End Sub
```

## 135 活頁簿另存新檔
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 活頁簿另存新檔()
    Dim fPath As String, fName As String
    fPath = "C:\ExcelVBA\"
    Workbooks.Open Filename:=fPath & "測試結果表單.xls"
    fName = Format(Date, "mm_dd") & "結果"
    
    ActiveWorkbook.Worksheets("結果").Name = fName
    ActiveWorkbook.SaveAs _
        Filename:=fPath & fName
End Sub
```

## 136 檢查活頁簿是否有巨集再存檔
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 檢查是否有巨集再存檔()
    Dim fPath As String, fName As String
    fPath = "C:\ExcelVBA\"
    fName = Format(Date, "mm_dd") & "結果"
    If ActiveWorkbook.HasVBProject Then
       ActiveWorkbook.SaveAs _
           Filename:=fPath & fName, _
           FileFormat:=xlOpenXMLWorkbookMacroEnabled
    Else
       ActiveWorkbook.SaveAs _
           Filename:=fPath & fName, _
           FileFormat:=xlOpenXMLWorkbook
    End If
End Sub
```

## 137 檢查活頁簿的修改是否已存檔
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 活頁簿的更新存檔確認()
    If ActiveWorkbook.Saved Then
        MsgBox "活頁簿不需存檔!!!"
    Else
        MsgBox "已修改!!更新存檔!!"
        ActiveWorkbook.Save
    End If
End Sub

Sub 所有的活頁簿不存檔直接關閉()
    Dim xBook As Workbook
    For Each xBook In Workbooks
        xBook.Saved = True
        xBook.Close
    Next
End Sub
```

## 138 開啟「另存新檔」對話方塊
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 指定檔名存檔()
    Dim fPath As String
    fPath = "C:\ExcelVBA\"
    Workbooks.Open Filename:=fPath & "測試結果表單.xls"
    With Application.FileDialog(msoFileDialogSaveAs)
        .FilterIndex = 1
        .InitialFileName = fPath & "互換\結果"
        If .Show = -1 Then .Execute
    End With
End Sub
```

## 139 複製活頁簿
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 複製活頁簿()
    Dim bkPath As String, bkName As String
    bkPath = "C:\ExcelVBA\BK\"
    bkName = Format(Date, "mm_dd") & "BK.xls"
    Workbooks.Open Filename:="C:\ExcelVBA\測試結果表單.xls"
    ActiveWorkbook.SaveCopyAs Filename:=bkPath & bkName
End Sub
```

## 140 保護活頁簿
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 保護活頁簿()
    ActiveWorkbook.Protect Password:="PassWord", _
                           Structure:=True, _
                           Windows:=True
End Sub

Sub 解除活頁簿()
    ActiveWorkbook.Unprotect Password:="PassWord"
End Sub
```

## 141 取得活頁簿的存檔路徑
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 活頁簿的存檔地點()
    Dim fPath As String
    On Error GoTo errHandler
    
    fPath = ActiveWorkbook.Path
    ActiveSheet.Copy
    ActiveWorkbook.SaveAs Filename:=fPath & "\" & Range("A1").Value & "xls"
    MsgBox ActiveWorkbook.Path
    Exit Sub
errHandler:
    MsgBox Err.Description
End Sub

Sub 活頁簿的存檔地點2()
    On Error GoTo errHandler
     
    ChDir ActiveWorkbook.Path
    ActiveSheet.Copy
    ActiveWorkbook.SaveAs _
        Filename:=Range("A1").Value & ".xls"
    MsgBox ActiveWorkbook.Path
    Exit Sub
errHandler:
    MsgBox Err.Description
End Sub
```

## 142 取得活頁簿名稱
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 取得簿名測試()
    With Application.FileDialog(msoFileDialogOpen)
        .FilterIndex = 2
        If .Show = 0 Then Exit Sub
        .Execute
    End With
    MsgBox "Name: " & ActiveWorkbook.Name & Chr(10) & _
           "FullName: " & ActiveWorkbook.FullName
End Sub
```

## 143 設定活頁簿的布景主題
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 設定佈景主題()
    Dim tPath As String
    tPath = "C:\Program Files\Microsoft Office\Document Themes 12\"
    ActiveWorkbook.ApplyTheme (tPath & "Verve.thmx")
End Sub

Sub 變更配色()
    Dim cPath As String
    cPath = "C:\Program Files\Microsoft Office\Document Themes 12\Theme Colors\"
    ActiveWorkbook.Theme.ThemeColorScheme.Load (cPath & "Opulent.xml")
End Sub

Sub 變更字型()
    Dim fPath As String
    fPath = "C:\Program Files\Microsoft Office\Document Themes 12\Theme Fonts\"
    ActiveWorkbook.Theme.ThemeFontScheme.Load (fPath & "Equity.xml")
End Sub

Sub 變更效果()
    Dim ePath As String
    ePath = "C:\Program Files\Microsoft Office\Document Themes 12\Theme Effects\"
    ActiveWorkbook.Theme.ThemeEffectScheme.Load (ePath & "Verve.eftx")
End Sub
```

## 144 開啟活頁簿時自動執行的程序
```
VERSION 1.0 CLASS
BEGIN
  MultiUse = -1  'True
END
Attribute VB_Name = "ThisWorkbook"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = True
Option Explicit

Private Sub Workbook_Open()
    Cells(Rows.Count, 1).End(xlUp).Offset(1).Value = Now
    MsgBox "打開[ " & Me.Name & " ]活頁簿!!"
End Sub
```

## 145 關閉活頁簿時自動執行的程序
```
VERSION 1.0 CLASS
BEGIN
  MultiUse = -1  'True
END
Attribute VB_Name = "ThisWorkbook"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = True

Private Sub Workbook_BeforeClose(Cancel As Boolean)
    Dim myRange As Range, myflag As Boolean
    myflag = False
    For Each myRange In Range("A7:C7")
        If myRange.Value = "" Then
           myflag = True
        End If
    Next
    If myflag Then
       MsgBox "有漏記!!!"
       Cancel = True
    Else
       ActiveWorkbook.SaveAs Format(Date, "mmdd") & Range("C2").Value
    End If
End Sub
```