# Ch03 資料操作

## 099 使用空格等將字串分割後分置於各欄
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 字串的分割()
    Range("A2:A5").TextToColumns Destination:=Range("B2"), _
    Space:=True, Comma:=True
End Sub
```

## 100 鍵入連續值給儲存格
```
Attribute VB_Name = "Module2"
Option Explicit

Sub AutoFill()
    Range("A3").AutoFill Destination:=Range("A3:A33"), Type:=xlFillSeries
    Range("B2").AutoFill Destination:=Range("B2:M2")
    Range("A3:A33").AutoFill Destination:=Range("A3:M33"), Type:=xlFillFormats
End Sub
```

## 101 改變資料的排序方式
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 排序()
    With ActiveSheet.Sort
        .SortFields.Clear
        .SortFields.Add Key:=Range("C2"), SortOn:=xlSortOnValues, _
         Order:=xlAscending
        .SortFields.Add Key:=Range("H2"), SortOn:=xlSortOnValues, _
         Order:=xlDescending
        .SetRange Range("A2:H27")
        .Header = xlYes
        .Apply
    End With
End Sub

Sub 使用者自定排序()
    With ActiveSheet.Sort
        .SortFields.Add Key:=Range("C2"), SortOn:=xlSortOnValues, _
            Order:=xlAscending, CustomOrder:="台北分店, 新竹分店, 台中分店, 高雄分店"
        .SetRange Range("A2:H27")
        .Header = xlYes
        .Apply
    End With
End Sub
```

## 102 做用自動篩選挑出符合條件的資料
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 自動篩選()
   Dim Joken1 As String, Joken2 As String
   
   Joken1 = "台北分店"
   Joken2 = "DVD*"
   Range("A2").AutoFilter Field:=3, Criteria1:=Joken1
   Range("A2").AutoFilter Field:=5, Criteria1:=Joken2
   
   MsgBox "分店:" & Joken1 & ", 商品名稱:" & Joken2 & " 滿足條件資料: " _
   & Range("A2").CurrentRegion.Columns(1). _
   SpecialCells(xlCellTypeVisible).Count - 1 & "筆 "
   
   Range("A2").AutoFilter
End Sub
```

## 103 做用準則範圍挑出資料
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 準則範圍()
    Dim xRange As Range, yRange As Range
    
    If ActiveSheet.FilterMode Then
       ActiveSheet.ShowAllData
    Else
       Set xRange = Range("A6").CurrentRegion
       Set yRange = Range("A1").CurrentRegion
       xRange.AdvancedFilter _
           Action:=xlFilterInPlace, CriteriaRange:=yRange
       Set xRange = Nothing: Set yRange = Nothing
    End If
End Sub

Sub 準則範圍2()
    Dim xRange As Range, yRange As Range
    Dim sName, allName
    Set xRange = Worksheets(1).Range("A6").CurrentRegion
    allName = Array("台北分店", "新竹分店", "台中分店", "高雄分店")
    For Each sName In allName
        Range("C2").Value = sName
        Set yRange = Worksheets(1).Range("A1").CurrentRegion
        xRange.AdvancedFilter Action:=xlFilterCopy, CriteriaRange:=yRange, _
                              CopyToRange:=Worksheets(sName).Range("A1")
    Next
    Set xRange = Nothing: Set yRange = Nothing
End Sub
```

## 104 建立表格
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 建立表格()
    If ActiveSheet.ListObjects.Count <> 0 Then
       MsgBox "已經做好表格!!"
    Else
       ActiveSheet.ListObjects.Add(xlSrcRange, _
           Range("A2").CurrentRegion, , xlYes).Name = "Table1"
       'Range("Table1").Select
    End If
End Sub

Sub 刪除表格()
    If ActiveSheet.ListObjects.Count <> 0 Then
        'ActiveSheet.ListObjects("Table1").TableStyle = ""
        ActiveSheet.ListObjects("Table1").Unlist
    End If
End Sub

Sub 排序和塞選()
    'With ActiveSheet.ListObjects("Table1").Sort
    '   .SortFields.Clear
    '  .SortFields.Add Key:=Range("H2"), _
    '        SortOn:=xlSortOnValues, Order:=xlDescending
    '    .Header = xlYes
    '    .Apply
    'End With
     ActiveSheet.ListObjects("Table1").Range.AutoFilter _
     Field:=3, Criteria1:="台北分店"
End Sub
```

## 105 改變表格的樣式
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 變更樣式()
    ActiveSheet.ListObjects("Table1").TableStyle = "TableStyleMedium5"
    MsgBox "變更為" & ActiveSheet.ListObjects("Table1").TableStyle.NameLocal _
           & "!!"
End Sub
```

## 106 在表格顯示合計列
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 合計()
    With ActiveSheet.ListObjects("Table1")
        .ShowTotals = True
        .ListColumns("單價").TotalsCalculation = xlTotalsCalculationCount
        .ListColumns("數量").TotalsCalculation = xlTotalsCalculationAverage
    End With
End Sub
```

## 107 製作樞紐分析表
```
Attribute VB_Name = "Module1"

Sub 製作樞紐分析表()
    Dim srcRange As Range
    Dim xPCach As PivotCache, xPTbl As PivotTable
    Set srcRange = Worksheets("Data").Range("A2").CurrentRegion
    Worksheets.Add After:=Worksheets(Sheets.Count)
    ActiveSheet.Name = "總計"
    
    Set xPCach = ActiveWorkbook.PivotCaches.Add _
            (SourceType:=xlDatabase, SourceData:=srcRange)
    Set xPTbl = xPCach.CreatePivotTable _
            (TableDestination:=Range("A3"), TableName:="Privot1")
    With xPTbl
        .PivotFields("分店").Orientation = xlColumnField
        .PivotFields("商品名").Orientation = xlRowField
        .PivotFields("金額").Orientation = xlDataField
    End With
End Sub
```

## 108 將樞紐分析表的資料欄位分組
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 日期分組()
    Range("A5").Group _
        Start:=True, End:=True, Periods:=Array(False, False, False, False, _
        True, False, False)
End Sub

Sub 製作樞紐分析表()
    Dim srcRange As Range
    Dim xPCach As PivotCache, xPTbl As PivotTable
    
    Set srcRange = Worksheets("Data").Range("A2").CurrentRegion
    Worksheets.Add After:=Worksheets(Sheets.Count)
    ActiveSheet.Name = "集計"
    Set xPCach = ActiveWorkbook.PivotCaches.Add _
            (SourceType:=xlDatabase, SourceData:=srcRange)
    
    Set xPTbl = xPCach.CreatePivotTable _
            (TableDestination:=Range("A3"), TableName:="Pivot1")
    
    With xPTbl
        .PivotFields("商品名").Orientation = xlColumnField
        .PivotFields("日期").Orientation = xlRowField
        .PivotFields("金額").Orientation = xlDataField
    End With
End Sub
```

## 109 查詢儲存格內容
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 執行查詢()
    Dim srcRange As Range, fndRange As Range
    Worksheets("查詢").Activate
    Set srcRange = Worksheets("Data").Range("A2").CurrentRegion.Columns(5)
    Set fndRange = srcRange.Find(what:=Range("B1").Value)
    If Not fndRange Is Nothing Then
       Cells(5, 1).Value = fndRange.Offset(, -4).Value
       Cells(5, 2).Value = fndRange.Offset(, -3).Value
       Cells(5, 3).Value = fndRange.Offset(, -2).Value
    Else
       MsgBox "沒有該商品!!"
    End If
End Sub
```
## 110 持續查詢儲存格內容
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 持續執行查詢()
    Dim srcRange As Range, fndRange As Range
    Dim fstAddress As String, i As Integer
    Worksheets("查詢").Activate
    Set srcRange = Worksheets("Data").Range("A2").CurrentRegion.Columns(5)
    Set fndRange = srcRange.Find(what:=Range("B1").Value)
    If Not fndRange Is Nothing Then
        fstAddress = fndRange.Address
        i = 5
        Do
            Cells(i, 1).Value = fndRange.Offset(, -4).Value
            Cells(i, 2).Value = fndRange.Offset(, -3).Value
            Cells(i, 3).Value = fndRange.Offset(, -2).Value
            Set fndRange = srcRange.FindNext(after:=fndRange)
            i = i + 1
        Loop Until fndRange.Address = fstAddress
    Else
       MsgBox "沒有該商品!!"
    End If
End Sub
```

## 111 將儲存格範圍中查出的字串置換為其他字串
```
Attribute VB_Name = "Module1"
Option Explicit

Sub 執行置換()
    Range("B2:B8").Replace What:="2003", Replacement:="2007"
    Range("B2:B8").Replace What:=".NET", Replacement:="2005"
End Sub

Sub 置換格式()
    Application.FindFormat.Interior.Color = RGB(153, 255, 153)
    Application.ReplaceFormat.Interior.Color = RGB(255, 255, 102)
    ActiveSheet.UsedRange.Replace What:="", Replacement:="", _
            SearchFormat:=True, ReplaceFormat:=True
End Sub
```